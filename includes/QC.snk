rule fastqc:
    input: get_fastq_input
    output:
        "fastqc/{sample}_{read}_fastqc.zip"
    threads: 4
    log:
        "logs/fastqc/{sample}_{read}.log"
    conda:
        f"../{config['envs']}/QC-env.yml"
    script:
        "../scripts/fastQC.py"


rule fastq_multiQC:
    input:
        get_fastqc_list
    output:
        "QC/fastQC.html"
    threads: 2
    conda:
        f"../{config['envs']}/QC-env.yml"
    shell:
        "multiqc -f -o QC/ -n fastQC --interactive fastqc/; "  # interactive for big number of files
        "rm -f fastqc/*_fastqc.html fastq/*.sub"  # leave the zip files for accumulated multiQC of all processed samples


# ####################### BAM QC ########################################

rule cover_BED:
    input:
        sample = "bamfinal/{sample}.bam",
        ref_gen = os.path.join(full_path('genome_path'), 'gen_ref.done')
    output: "coverBED/{sample}.txt"
    log: "logs/coverBED/{sample}.log"
    threads: 2
    params:
        exon_cover = full_path('bed_file'),
        prettifyBed = get_shell('prettyBED.awk'),
        fastq = get_fastq_pair,
        refgen = full_path('genome')
    conda:
        f"../{config['envs']}/QC-env.yml"
    script:
        "../scripts/bedcover.py"


rule bam_metrix:
    input:
        bam = "bamfinal/{sample}.bam"
    output: 
        "bam_metrics/{sample}.metrics"
    conda:
        f"../{config['envs']}/align-env.yml"
    log:
        "logs/bam_metrix/{sample}.log"
    threads: config['bam_metrix']['threads']
    params:
        refgen = full_path('genome'),
        bait = full_path("bed_file"),
        target = full_path("bed_file_targets")
    shell:
        "picard CollectHsMetrics I={input.bam} O={output} R={params.refgen} "
        "BAIT_INTERVALS={params.bait} "
        "TARGET_INTERVALS={params.target}"

rule bam_multiQC:
    input:
        expand("bam_metrics/{samples}.metrics", samples=sample_df.index)
    output:
        "QC/libraryQC.html"
    conda:
        f"../{config['envs']}/QC-env.yml"
    threads: 2
    shell:
        "multiqc -f -o QC/ -n libraryQC --interactive bam_metrics/; "


# ##################### insert size QC ##############################################
rule insert_metrix:
    input:
        bam = "bamfinal/{sample}.bam"
    output:
        metric = "insert_metrics/{sample}.metrics",
        pdf = "insert_metrics/{sample}.metrics.pdf"
    conda:
        f"../{config['envs']}/align-env.yml"
    log:
        "logs/bam_metrix/{sample}.log"
    threads: config['bam_metrix']['threads']
    shell:
        "picard CollectInsertSizeMetrics I={input.bam} O={output.metric} H={output.pdf} M=0.5"


rule insert_multiQC:
    input:
        expand("insert_metrics/{samples}.metrics", samples=sample_df.index)
    output:
        "QC/insertQC.html"
    conda:
        f"../{config['envs']}/QC-env.yml"
    threads: 2
    shell:
        "multiqc -f -o QC/ -n insertQC --interactive insert_metrics/; "