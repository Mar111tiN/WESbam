# ################# FILTER ###################################################
rule filter_variants:
    input: "filter/{sample}_{tumor}-{norm}.csv"
    output: "filter/{sample}_{tumor}-{norm}.{filter}.csv"
    wildcard_constraints:
        sample = "[^_]+"
    conda:
        "../env/filter-env.yml"
    threads:
        config['filter']['threads']
    script:
        "../scripts/filter.py"


rule filter_bam:
    input:
        csv = "filter/{sample}_{tumor}-{normal}.{filter}.csv",
        tumor_bam = "recalib/{sample}_{tumor}.bam",
        tumor_bai = "recalib/{sample}_{tumor}.bai",
        normal_bam = "recalib/{sample}_{normal}.bam",
        normal_bai = "recalib/{sample}_{normal}.bai",
    output:
        # tumor_bam = "filter_bam/{sample}_{tumor}.{filter}.bam",
        # tumor_bai = "filter_bam/{sample}_{tumor}.{filter}.bai",
        # normal_bam = "filter_bam/{sample}_{normal}.{filter}.bam",
        # normal_bai = "filter_bam/{sample}_{normal}.{filter}.bai",
        done = "filter_bam/{sample}_{tumor}-{normal}.{filter}.done"
    conda:
        "../env/align-env.yml"
    threads:
        config['filter_bam']['threads']
    params:
        mut_bed_file = get_mut_bed,
        tumor_bam = lambda w, input: input.tumor_bam.replace(os.path.split(input.tumor_bam)[0], "filter_bam").replace(".bam", f".{w.filter}.bam"),
        normal_bam = lambda w, input: input.normal_bam.replace(os.path.split(input.normal_bam)[0], "filter_bam").replace(".bam", f".{w.filter}.bam")
    shell:
        "samtools view -bhL {params.mut_bed_file} {input.tumor_bam} > {params.tumor_bam}; "
        "picard BuildBamIndex INPUT={params.tumor_bam}; "
        "samtools view -bhL {params.mut_bed_file} {input.normal_bam} > {params.normal_bam}; "
        "picard BuildBamIndex INPUT={params.normal_bam}; "
        "rm -f filter_bam/{params.mut_bed_file}; "
        "touch {output.done}"

rule IGVnav:
    input:
        "filter/{sample}_{tumor}-{normal}.{filter}.csv"
    output:
        "filter_bam/{sample}_{tumor}-{normal}.{filter}.IGVnav.txt"
    threads:
        1
    run:
        filter_file = str(input).replace('.csv', '.moderate.csv')
        print(filter_file)
        df = pd.read_csv(filter_file, sep='\t', index_col=False).iloc[:, :5]
        print(f'Loaded {filter_file}')
        for col in ['Call', 'Tags', 'Notes']:
            df[col] = ''
        df.loc[:, 'Chr'] = df['Chr'].str.replace('chr', '')
        df.to_csv(str(output), sep='\t', index=False)
        print(f"Written to {str(output)}")

rule filter_pileup:
    input:
        "filter_bam/{sample}_{tumor}-{normal}.{filter}.done"
    output:
        "filter_pileup/{sample}_{tumor}-{normal}.{filter}.pileup"
    threads:
        2
    conda:
        "../env/align-env.yml"
    params:
        input = lambda w: f"filter_bam/{w.sample}_{w.tumor}.{w.filter}.bam filter_bam/{w.sample}_{w.normal}.{w.filter}.bam",
        refgen = full_path('genome'),
        qual = f"-q {config['mpileup']['MAPQ']} -Q {config['mpileup']['Q']}",
        cleanpileup = get_script('cleanpileup')
    shell:
        "samtools mpileup -f {params.refgen} {params.qual} {params.input} | {params.cleanpileup} > {output}"


rule detect_HDR:
    input:
        filter_file = "filter/{sample}_{tumor}-{normal}.{filter}.csv",
        filter_bam = "filter_bam/{sample}_{tumor}-{normal}.{filter}.done",
        filter_pileup = "filter_pileup/{sample}_{tumor}-{normal}.{filter}.pileup"
    output:
        "filter/{sample}_{tumor}-{normal}.{filter}.HDR.csv"
    conda:
        "../env/HDR-env.yml"
    threads:
        2
    params:
        min_sim = config['HDR']['min_similarity']
    script:
        "../scripts/HDR.py"
