# ################# FILTER ###################################################
rule filter1:
    input: "filter/{sample}_{tumor}-{normal}.csv"
    output: 
        filter1 = "table/{sample}_{tumor}-{normal}.filter1.csv",
        basic = "filter/{sample}_{tumor}-{normal}.basic.csv"
    wildcard_constraints:
        sample = "[^_]+"
    threads:
        config['filter']['threads']
    script:
        "../scripts/filter1.py" 


rule primer3:
    input: "table/{sample}_{tumor}-{normal}.filter1.csv"
    output: "table/{sample}_{tumor}-{normal}.filter1.primer.csv"
    conda:
        f"../{config['envs']}/primer3-env.yml"
    threads:
        config['primer3']['threads']
    params:
        genome_split = full_path('genome_split')
    script:
        "../scripts/primer.py"


rule filter_bam:
    input:
        csv = "table/{sample}_{tumor}-{normal}.{filter}.csv",
        bam = "bamfinal/{sample}_{type}.bam",
    output:
        bamdone = "filterbamdone/{sample}_{tumor}-{normal}.{type}.{filter}.done"
    conda:
        f"../{config['envs']}/align-env.yml"
    threads:
        config['filter_bam']['threads']
    params:
        folder = config['filter_bam']['folder'],
        mut_bed_file = get_mut_bed,
        out_bam = lambda w, output: output.bamdone.replace("bamdone", "bam").replace(f"{w.tumor}-{w.normal}.", "").replace(".done", f".bam")
    shell:
        "mkdir -p {params.folder}; "
        "samtools view -bhL {params.mut_bed_file} {input.bam} > {params.out_bam}; "
        "picard BuildBamIndex INPUT={params.out_bam}; "
        "rm -f {params.mut_bed_file}; "
        "touch {output.bamdone}"


rule combine_filter1:
    input:
        filter1 = "table/{sample}_{tumor}-{normal}.filter1.csv",
        primer = "table/{sample}_{tumor}-{normal}.filter1.primer.csv",
        HDR = "table/{sample}_{tumor}-{normal}.filter1.HDR.csv"
    output: "filter/{sample}_{tumor}-{normal}.filter1.csv"
    threads: 1
    run:
        ## merge the files
        show_output(f'Merging {input.primer} and {input.HDR} into {input.filter1}..')
        filter1_df = pd.read_csv(input.filter1, sep='\t')
        HDR_df = pd.read_csv(input.HDR, sep='\t')
        primer_df = pd.read_csv(input.primer, sep='\t')
        merge = pd.merge(filter1_df, HDR_df, how='inner', on=['Chr', 'Start', 'End', 'Ref', 'Alt', 'Gene'])
        merge_df = pd.merge(merge, primer_df, how='inner', on=['Chr', 'Start', 'End', 'Ref', 'Alt', 'Gene'])

        output = str(output)
        show_output(f"Writing merged filter1-list to {output}.", time=False)
        merge_df.to_csv(output, sep='\t', index = False)


rule filter2:
    input: 
        filter1 = "filter/{sample}_{tumor}-{normal}.filter1.csv"
    output: 
        # filter2.loose is a placeholder for all the filter2 files:
        #  loose.csv
        #  moderate.csv
        #  strict.csv
        filter2 = "filter/{sample}_{tumor}-{normal}.filter2.loose.csv",
        filter2_for_filterbam = "table/{sample}_{tumor}-{normal}.filter2.csv"
    threads: 
        1
    script:
        f"../scripts/filter2.py" 


rule IGVnav:
    input:
        filter = "filter/{sample}_{tumor}-{normal}.filter2.loose.csv",
        bam = "filterbamdone/{sample}_{tumor}-{normal}.{normal}.filter2.done"
    output:
        filterbam = "filterbam/{sample}_{tumor}-{normal}.filter2.IGVnav.txt"
    threads:
        1
    run:
        # selectinng the right filter2 file from the configs
        filter_file = input.filter.replace(".loose.csv", f".{config['filter_bam']['stringency_for_IGVnav']}.csv")
        df = pd.read_csv(filter_file, sep='\t', index_col=False).iloc[:, :5]
        print(f'Loaded {filter_file}')
        for col in ['Call', 'Tags', 'Notes']:
            df[col] = ''
        df.loc[:, 'Chr'] = df['Chr'].str.replace('chr', '')
        df.to_csv(str(output), sep='\t', index=False)
        print(f"Written to {output.filterbam}")


